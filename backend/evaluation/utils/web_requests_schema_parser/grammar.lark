COMMENT: "#" /[^\n]/*
PRIMITIVE.2: "str" | "int" | "float"
OBJECT.2: "object"
TYPEDEF.2: "typedef"
HTTP_METHOD: "GET"i | "HEAD"i | "POST"i | "PUT"i | "DELETE"i | "CONNECT"i | "OPTIONS"i | "TRACE"i | "PATCH"i
REQUEST: "->"
RESPONSE: "<-"
SEPARATOR: ":"
OPTIONAL: "?"
ARRAY: "[]"
IDENTIFIER: /[a-zA-Z][a-zA-Z0-9_]*/

?start          : block*
block           : communication
                | typedef
communication   : IDENTIFIER _BEGIN  request response _END
request         : REQUEST request_body
request_body    : _BEGIN (HTTP_METHOD body)+ _END
response        : RESPONSE body?
body            : _BEGIN attribute* _END
attribute       : OPTIONAL? IDENTIFIER ARRAY? SEPARATOR type [body]
type            : (PRIMITIVE | enum | global_type | object)
enum            : IDENTIFIER ("," IDENTIFIER)*
global_type     : "$" IDENTIFIER
object          : OBJECT IDENTIFIER
typedef         : TYPEDEF object body

%declare _BEGIN _END

_NL: /(\r?\n[\t ]*)+/

%import common.WS_INLINE
%ignore WS_INLINE
%ignore COMMENT